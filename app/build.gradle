apply plugin: 'com.android.application'
def static BuildTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+8"))
}

def keystorePropertiesFile = rootProject.file("keystore.properties")

def keystoreProperties = new Properties()

keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
android {
    compileSdkVersion rootProject.ext.compileSdk
    buildToolsVersion rootProject.ext.buildTools
    defaultConfig {
        applicationId 'com.chenghe.parttime'
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        manifestPlaceholders = [UMENG_CHANNEL: "PARTTIME"]
        // dex突破65535的限制
        multiDexEnabled true
        flavorDimensions "1"
        ndk {
            //APP的build.gradle设置支持的SO库架构
            abiFilters "armeabi", "armeabi-v7a", "arm64-v8a", "x86"
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }
    //jdk版本
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    repositories {
        flatDir {
            dirs 'libs'   // aar目录
        }
    }
    //执行lint检查，有任何的错误或者警告提示，都会终止构建，我们可以将其关掉。
//    lintOptions {
//        abortOnError false
//    }
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.config
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
//            applicationVariants.all { variant ->
//                variant.outputs.all {
//                    // 输出apk名称为app_kuan_v1.0_2015.01.15.apk
//                    outputFileName = "app_${variant.productFlavors[0].name}_v${variant.versionName}_${BuildTime()}.apk"
//                }
//            }
        }
        debug {
            signingConfig signingConfigs.config
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    /*配置渠道*/
    productFlavors {
        PARTTIME {//官方渠道

        }
        Tencent {//投放应用宝市场
        }
        Baidu {//投放百度市场
        }
        Wandoujia {//投放豌豆荚市场
        }
        Vivo {//投放vivo市场
        }
        Oppo {//投放oppo市场
        }
        Xiaomi {//投放小米市场
        }
        Meizu {//投放魅族市场
        }
        Huawei {//投放华为应用市场
        }
        Lenovo {//投放联想市场
        }
        Letv {//投放乐视市场
        }
        Gionee {//投放金立市场
        }
        HiMarket {//投放安卓市场
        }
    }
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    implementation project(path: ':base')


    annotationProcessor("com.jakewharton:butterknife-compiler:${rootProject.ext.butterknife}") {
        exclude module: 'appcompat-v7'
        exclude group: 'com.android.support'
    }
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.0-alpha-31'
    implementation 'com.hyman:flowlayout-lib:1.1.2'
    implementation  'com.umeng.umsdk:analytics:8.0.0'
    implementation  'com.umeng.umsdk:common:2.0.0'
}
